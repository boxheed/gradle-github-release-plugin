buildscript {
  repositories {
    mavenCentral()
    maven {
      url = "https://artifacts.fizzpod.com/public/mvn/"
    }
    maven {
      url = "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath ('com.fizzpod:gradle-plugin-opinion:24.0.1') {
      exclude group: 'com.fizzpod', module: 'gradle-github-release-plugin'
    }
  }
}

apply plugin: 'com.fizzpod.pater-build'
apply plugin: 'com.fizzpod.github-release'

sourceSets {
  main {
    java {
      srcDir 'buildSrc/src/main/java'
    }
    resources {
      srcDir 'buildSrc/src/main/resources'
      exclude 'META-INF/gradle-plugins/com.fizzpod.github-release.properties'
    }
    groovy {
      srcDir 'buildSrc/src/main/groovy'
    }
  }
  test {
    java {
      srcDir 'buildSrc/src/test/java'
    }
    resources {
      srcDir 'buildSrc/src/test/resources'
    }
    groovy {
      srcDir 'buildSrc/src/test/groovy'
    }
  }
}

dependencies {
  api gradleApi()
  api localGroovy()
  implementation 'com.squareup.okhttp3:okhttp:5.0.0'
  implementation 'org.kohsuke:github-api:1.327'
  implementation 'org.apache.tika:tika-core:3.2.0'

  //########################################################
  //overriding the provided versions of these libraries as 
  //they are not compatible with the dependency check plugin
  implementation 'org.apache.commons:commons-lang3:3.17.0'
  implementation 'org.apache.commons:commons-text:1.13.1'
  //########################################################
  
  
}

/*
* The following configuration is specified in the gradle-plugin-opinion
githubRelease {
  token project.hasProperty("github_release_token")? github_release_token: ""
  repo scminfo.origin
  tagName {'v' + project.version}
  releaseName {}'Release ' + project.version}
  generateReleaseNotes true
  releaseAssets { ->
		jar.outputs.files
	}
}
*/

gradlePlugin {
  plugins {
    plugin {
      description = 'Gradle plugin to create a Github release and upload artefacts.'
      tags.set(['github', 'release', 'artefact'])
      id = 'com.fizzpod.github-release'
      displayName = 'Gradle Github Release Plugin'
      implementationClass = 'com.fizzpod.gradle.plugins.githubrelease.GithubReleasePlugin'
    }
  }
}
